<Inicial> ::= <ListaClases>

<ListaClases> ::= <Clase> <ListaClasesAux>
<ListaClasesAux> ::= <Clase> <ListaClasesAux>
<ListaClasesAux> ::= ε

<Clase> ::= class idclase <Herencia> { <ListaMiembros> }
<Herencia> ::= extends idclase
<Herencia> ::= ε

<ListaMiembros> ::= <Miembro> <ListaMiembros>
<ListaMiembros> ::= ε

<Miembro> ::= <AtributoConVisibilidadExplicita>
<Miembro> ::= <ConstructorOAtributoNoEstaticoConVisibilidadImplicita>
<Miembro> ::= <MetodoEstaticoOAtributoEstaticoConVisibilidadImplicita>
<Miembro> ::= <MetodoDinamico>

<AtributoConVisibilidadExplicita> ::= <VisibilidadExplicita> <FormaAtributo> <Tipo> <IdentificadoresDeAtributo>

<ConstructorOAtributoNoEstaticoConVisibilidadImplicita> ::= idclase <ConstructorOAtributoAux>
<ConstructorOAtributoNoEstaticoConVisibilidadImplicita> ::= <TipoPrimitivo> <IdentificadoresDeAtributo>
<ConstructorOAtributoAux> ::= <ArgsFormales> <Bloque>
<ConstructorOAtributoAux> ::= <IdentificadoresDeAtributo>

<MetodoEstaticoOAtributoEstaticoConVisibilidadImplicita> ::= static <MetodoOAtributoAux>
<MetodoOAtributoAux> ::= void <IdentificadorParametrosYDefinicionDeMetodo>
<MetodoOAtributoAux> ::= <Tipo> idmetvar <MetodoOAtributoAuxAux>
<MetodoOAtributoAuxAux> ::= <ArgsFormales> <Bloque>
<MetodoOAtributoAuxAux> ::= <ListaDecAtrsAux> ;

<MetodoDinamico> ::= dynamic <TipoMetodo> <IdentificadorParametrosYDefinicionDeMetodo>

<IdentificadorParametrosYDefinicionDeMetodo> ::= idmetvar <ArgsFormales> <Bloque>

<FormaAtributo> ::= static
<FormaAtributo> ::= ε

<IdentificadoresDeAtributo> ::= <ListaDecAtrs> ;

<VisibilidadExplicita> ::= public
<VisibilidadExplicita> ::= private

<Tipo> ::= <TipoPrimitivo>
<Tipo> ::= idclase

<TipoPrimitivo> ::= boolean
<TipoPrimitivo> ::= char
<TipoPrimitivo> ::= int
<TipoPrimitivo> ::= string

<ListaDecAtrs> ::= idmetvar <ListaDecAtrsAux>
<ListaDecAtrsAux> ::= , idmetvar <ListaDecAtrsAux>
<ListaDecAtrsAux> ::= ε

<TipoMetodo> ::= <Tipo>
<TipoMetodo> ::= void

<ArgsFormales> ::= ( <ListaArgsFormalesOVacio> )
<ListaArgsFormalesOVacio> ::= <ListaArgsFormales>
<ListaArgsFormalesOVacio> ::= ε
<ListaArgsFormales> ::= <ArgFormal> <ListaArgsFormalesAux>
<ListaArgsFormalesAux> ::= , <ArgFormal> <ListaArgsFormalesAux>
<ListaArgsFormalesAux> ::= ε
<ArgFormal> ::= <Tipo> idmetvar

<Bloque> ::= { <ListaSentencias> }

<ListaSentencias> ::= <Sentencia> <ListaSentencias>
<ListaSentencias> ::= ε

<Sentencia> ::= ;
<Sentencia> ::= <VarLocalConTipoPrimitivo> ;
<Sentencia> ::= <VarLocalConTipoDeClaseOAccesoVarMetEstatico> ;
<Sentencia> ::= <Return_> ;
<Sentencia> ::= <If_>
<Sentencia> ::= <For_>
<Sentencia> ::= <Bloque>
<Sentencia> ::= <AsignacionOLlamadaNoEstaticos> ;

<AsignacionOLlamadaNoEstaticos> ::= <AccesoExcluyendoEstaticos> <AsignacionOLlamadaAux>
<AsignacionOLlamadaAux> ::= <TipoDeAsignacion>
<AsignacionOLlamadaAux> ::= ε
<Asignacion> ::= <AccesoIncluyendoEstaticos> <TipoDeAsignacion>

<TipoDeAsignacion> ::= = <Expresion>
<TipoDeAsignacion> ::= ++
<TipoDeAsignacion> ::= --

<VarLocal> ::= <Tipo> <VarLocalIdentificadorYDefinicion>
<VarLocalIdentificadorYDefinicion> ::= idmetvar <VarLocalAux>
<VarLocalConTipoPrimitivo> ::= <TipoPrimitivo> <VarLocalIdentificadorYDefinicion>

<VarLocalAux> ::= = <Expresion>
<VarLocalAux> ::= ε

<VarLocalConTipoDeClaseOAccesoVarMetEstatico> ::= idclase <VarLocalConTipoDeClaseOAccesoVarMetEstaticoAux>
<VarLocalConTipoDeClaseOAccesoVarMetEstaticoAux> ::= <VarLocalIdentificadorYDefinicion>
<VarLocalConTipoDeClaseOAccesoVarMetEstaticoAux> ::= <VarOMetodoEncadenado> <Encadenado> <AsignacionOLlamadaAux>

<Return_> ::= return <ExpresionOVacio>

<ExpresionOVacio> ::= <Expresion>
<ExpresionOVacio> ::= ε

<If_> ::= if ( <Expresion> ) <Sentencia> <Else_>
<Else_> ::= else <Sentencia>
<Else_> ::= ε

<For_> ::= for ( <VarLocal> ; <Expresion> ; <Asignacion> ) <Sentencia>

<ExpresionSinAccesoEstatico> ::= <ExpresionUnariaSinAccesoEstatico> <ExpresionBinaria>
<Expresion> ::= <ExpresionUnariaConAccesoEstatico> <ExpresionBinaria>

<ExpresionBinaria> ::= <OperadorBinario> <ExpresionUnariaConAccesoEstatico> <ExpresionBinaria>
<ExpresionBinaria> ::= ε

<OperadorBinario> ::= ||
<OperadorBinario> ::= &&
<OperadorBinario> ::= ==
<OperadorBinario> ::= !=
<OperadorBinario> ::= <
<OperadorBinario> ::= >
<OperadorBinario> ::= <=
<OperadorBinario> ::= >=
<OperadorBinario> ::= +
<OperadorBinario> ::= -
<OperadorBinario> ::= *
<OperadorBinario> ::= /
<OperadorBinario> ::= %

<ExpresionUnariaConAccesoEstatico> ::= <OperadorUnario> <OperandoConAccesoEstatico>
<ExpresionUnariaConAccesoEstatico> ::= <OperandoConAccesoEstatico>
<ExpresionUnariaSinAccesoEstatico> ::= <OperadorUnario> <OperandoConAccesoEstatico>
<ExpresionUnariaSinAccesoEstatico> ::= <OperandoSinAccesoEstatico>

<OperadorUnario> ::= +
<OperadorUnario> ::= -
<OperadorUnario> ::= !

<OperandoSinAccesoEstatico> ::= <Literal>
<OperandoSinAccesoEstatico> ::= <AccesoExcluyendoEstaticos>
<OperandoConAccesoEstatico> ::= <Literal>
<OperandoConAccesoEstatico> ::= <AccesoIncluyendoEstaticos>

<Literal> ::= null
<Literal> ::= true
<Literal> ::= false
<Literal> ::= intliteral
<Literal> ::= charliteral
<Literal> ::= stringliteral

<AccesoIncluyendoEstaticos> ::= <AccesoExcluyendoEstaticos>
<AccesoIncluyendoEstaticos> ::= <AccesoVarOMetodoEstatico>
<AccesoExcluyendoEstaticos> ::= <PrimarioSinExpresionParentizadaYSinAccesoEstatico> <Encadenado>
<AccesoExcluyendoEstaticos> ::= <CastingOExpresionParentizada> <Encadenado>

<CastingOExpresionParentizada> ::= ( <CastingOExpresionParentizadaAux>
<CastingOExpresionParentizadaAux> ::= idclase <CastingOExpresionParentizadaAuxAux>
<CastingOExpresionParentizadaAux> ::= <ExpresionSinAccesoEstatico> )
<CastingOExpresionParentizadaAuxAux> ::= ) <PrimarioConExpresionParentizada>
<CastingOExpresionParentizadaAuxAux> ::= <VarOMetodoEncadenado> <Encadenado> )

<PrimarioConExpresionParentizada> ::= <ExpresionParentizada>
<PrimarioConExpresionParentizada> ::= <PrimarioSinExpresionParentizada>
<PrimarioSinExpresionParentizada> ::= <PrimarioSinExpresionParentizadaYSinAccesoEstatico>
<PrimarioSinExpresionParentizada> ::= <AccesoVarOMetodoEstatico>

<AccesoVarOMetodoEstatico> ::= idclase <VarOMetodoEncadenado> <Encadenado>

<PrimarioSinExpresionParentizadaYSinAccesoEstatico> ::= <AccesoThis>
<PrimarioSinExpresionParentizadaYSinAccesoEstatico> ::= <AccesoConstructor>
<ExpresionParentizada> ::= ( <Expresion> )
<PrimarioSinExpresionParentizadaYSinAccesoEstatico> ::= <AccesoVarOMetodo>

<AccesoVarOMetodo> ::= idmetvar <AccesoVarOMetodoAux>
<AccesoVarOMetodoAux> ::= <ArgsActuales>
<AccesoVarOMetodoAux> ::= ε

<AccesoThis> ::= this

<AccesoConstructor> ::= new idclase <ArgsActuales>

<ArgsActuales> ::= ( <ListaExpsOVacio> )
<ListaExpsOVacio> ::= <ListaExps>
<ListaExpsOVacio> ::= ε
<ListaExps> ::= <Expresion> <ListaExpsAux>
<ListaExpsAux> ::= , <Expresion> <ListaExpsAux>
<ListaExpsAux> ::= ε

<Encadenado> ::= <VarOMetodoEncadenado> <Encadenado>
<Encadenado> ::= ε

<VarOMetodoEncadenado> ::= . idmetvar <VarOMetodoEncadenadoAux>
<VarOMetodoEncadenadoAux> ::= <ArgsActuales>
<VarOMetodoEncadenadoAux> ::= ε